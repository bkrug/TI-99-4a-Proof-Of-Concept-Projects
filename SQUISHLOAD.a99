       DEF  LTEST
       REF  LOADER,VMBW

* Use this program in order to load all
* of the files needed to test SQUISH.

PABBUF EQU  >1000
PAB    EQU  >F80
STATUS EQU  >837C
PNTR   EQU  >8356
* Byte 0 = Open
* Byte 1 = Status/Display/Fixed
* Byte 4 = max record length 80
* Byte 5 = actual length to write
* Byte 6-7 are not relevant
* Byte 8 = status o file
* Byte 9 = file name length
PDATA1 DATA >0004,PABBUF,>5000,>0000,>000F
FILEN1 TEXT 'DSK2.SQUISH.OBJ'
       BSS  7
PDATA2 DATA >0004,PABBUF,>5000,>0000,>0012
FILEN2 TEXT 'DSK2.SPRITEDAT.OBJ'
       BSS  4
PDATA3 DATA >0004,PABBUF,>5000,>0000,>0013
FILEN3 TEXT 'DSK2.SPRITEMAP2.OBJ'
       BSS  3
PDATA4 DATA >0004,PABBUF,>5000,>0000,>0013
FILEN4 TEXT 'DSK2.SPRITMUSIC.OBJ'
       BSS  3
PDATAE

LTEST  
* Store Screen position in R7
       CLR  R7
* Store file information in R3,R4, and R5
       LI   R3,PAB
       LI   R4,PDATA1
       LI   R5,PDATA2-PDATA1
* Write filename to screen
LTEST1 MOV  R7,R0
       MOV  R4,R1
       AI   R1,10
       MOV  @8(4),R2
       BLWP @VMBW
	   AI   R7,32
* Write PAB to VDP
       MOV  R3,R0
       MOV  R4,R1
       MOV  R5,R2
       BLWP @VMBW
*
       LI   R6,PAB+9
       MOV  R6,@PNTR
* Load the assembled code
       BLWP @LOADER
* If the Eqaul bit is set, report error
       JEQ  RPTERR
* Update R1 so we can load the next file
       A    R5,R4
       CI   R4,PDATAE
       JL   LTEST1
* Enter the test program
       B    @RUNTST
*
*
ERRMSG TEXT 'LOADER routine error: '
ERRCD  BSS  1
ERRCD1 EVEN
ERRFL  TEXT 'File: '
ERRFL1
*
RPTERR
* Add error code to message
       AI   R0,>3000
       MOVB R0,@ERRCD
* Display error message
       MOV  R7,R0
       LI   R1,ERRMSG
       LI   R2,ERRCD1-ERRMSG
       BLWP @VMBW
* Display particular file to fail
       AI   R0,>20
       LI   R1,ERRFL
       LI   R2,ERRFL1-ERRFL
       BLWP @VMBW
       A    R2,R0
       MOV  R4,R1
       AI   R1,10
       MOV  @8(4),R2
       BLWP @VMBW
JMP    LIMI 2
       LIMI 0
       JMP  JMP

       TEXT 'LOOKHERE'
*
* First loaded program will be found here:
RUNTST
       END